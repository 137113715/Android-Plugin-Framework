apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 2
        versionName "0.0.37-snapshot"
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        textReport false
        htmlReport false
        xmlReport false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    //这个依赖不是必须, 引入这个库是为了增强插件间通信能力
    //https://github.com/limpoxe/Android-ServiceManager
    compile 'com.limpoxe.support:android-servicemanager:1.0.5@aar'
}

tasks.whenTaskAdded { task ->
    android.buildTypes.all { buildType ->
        if (task.name.equalsIgnoreCase("bundle" + buildType.name)) {
            task.doLast {
                println '导出 ' + buildType.name + ' jar包到build/outputs/aar目录'
                project.copy {
                    from(buildDir.absolutePath + '/intermediates/bundles/' + buildType.name) {
                        include "classes.jar"
                    }
                    into(buildDir.absolutePath + "/outputs/aar")
                    rename('classes', project.name + "-" + buildType.name)
                }
            }
        }
    }
}

apply from: "./jcenter.gradle"